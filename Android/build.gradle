// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        jcenter()
        maven{
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta7'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
        maven{
            url 'https://maven.google.com'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

project.ext{
    supportLibraryVersion = '25.4.0'
}

subprojects { project ->

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion '6.0'
    }

    task checkstyle(type: Checkstyle) {
        group 'verification'
        configFile rootProject.file('checkstyle.xml')
        source = ['src']

        ignoreFailures false
        showViolations true
        include '**/*.java'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        classpath = files()
        configProperties = [
                'proj.module.dir'      : projectDir.absolutePath,
                'checkstyle.cache.file': './build/cache/checkstyle-cache'
        ]
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}